# Arguments
# 1st arg: X (one column time series)
# 2nd arg: max_func_invoc
# 3rd arg: p (non-seasonal AR order)
# 4th arg: d (non-seasonal differencing order)
# 5th arg: q (non-seasonal MA order)
# 6th arg: P (seasonal AR order)
# 7th arg: D (seasonal differencing order)
# 8th arg: Q (seasonal MA order)
# 9th arg: s (period in terms of number of time-steps)
# 10th arg: 0/1 (1 means include.mean)
# 11th arg: file name to store learnt parameters

#changing to additive sar since R's arima seems to do that

arima_css = function(Matrix[Double] w, Matrix[Double] X, Integer p, Integer P, Integer q, Integer Q, Integer s) return (Double obj){
	b = X[,2:ncol(X)]%*%w
	
	R = Rand(rows=nrow(X), cols=nrow(X), min=0, max=0)
	for(i7 in 1:q){
		ma_ind_ns = P+p+i7
		err_ind_ns = i7
		ones_ns = Rand(rows=nrow(R)-err_ind_ns, cols=1, min=1, max=1)
		d_ns = ones_ns * castAsScalar(w[ma_ind_ns,1])
		R[1+err_ind_ns:nrow(R),1:ncol(R)-err_ind_ns] = R[1+err_ind_ns:nrow(R),1:ncol(R)-err_ind_ns] + diag(d_ns)
	}
	for(i8 in 1:Q){
		ma_ind_s = P+p+q+i8
		err_ind_s = s*i8
		ones_s = Rand(rows=nrow(R)-err_ind_s, cols=1, min=1, max=1)
		d_s = ones_s * castAsScalar(w[ma_ind_s,1])
		R[1+err_ind_s:nrow(R),1:ncol(R)-err_ind_s] = R[1+err_ind_s:nrow(R),1:ncol(R)-err_ind_s] + diag(d_s)
	}
	
	#checking for strict diagonal dominance
	check = sum(ppred(rowSums(abs(R)), 1, ">="))
	if(check > 0){
		print("R is not diagonal dominant. Suggest switching to an exact solver.")
	}
	
	max_iter = 100
	tol = 0.01

	y_hat = Rand(rows=nrow(X), cols=1, min=0, max=0)
	iter = 0
  	diff = tol+1.0
	while(iter < max_iter & diff > tol){
    	y_hat_new = b - R%*%y_hat
    	diff = sum((y_hat_new-y_hat)*(y_hat_new-y_hat))
    	y_hat = y_hat_new
    	iter = iter + 1

    	#print("Inner iter " + iter + ": " + diff)
  	}
  	print("Inner loop " + iter + ": " + diff)
  
  	errs = X[,1] - y_hat
  	obj = sum(errs*errs)
}

#input col of time series data
X = read($1)

max_func_invoc = $2

#non-seasonal order
p = $3
d = $4
q = $5

#seasonal order
P = $6
D = $7
Q = $8

#length of the season
s = $9

include_mean = $10

num_rows = nrow(X)

if(num_rows <= d){
	print("non-seasonal differencing order should be larger than length of the time-series")
}

Y = X
for(i in 1:d){
	n1 = nrow(Y)+0.0
	Y = Y[2:n1,] - Y[1:n1-1,]
}

num_rows = nrow(Y)+0.0
if(num_rows <= s*D){
	print("seasonal differencing order should be larger than number of observations divided by length of season")
}

for(i in 1:D){
	n1 = nrow(Y)+0.0
	Y = Y[s+1:n1,] - Y[1:n1-s,]
}

n = nrow(Y)

max_ar_col = P+p
max_ma_col = Q+q
if(max_ar_col > max_ma_col){
	max_arma_col = max_ar_col
}else{
	max_arma_col = max_ma_col
}

mu = 0
if(include_mean == 1){
	mu = sum(Y)/nrow(Y)
	Y = Y - mu
}

totcols = 1+p+P+Q+q #target col (X), p-P cols, q-Q cols  

Z = Rand(rows=n, cols=totcols, min=0, max=0)
Z[,1] = Y #target col

parfor(i1 in 1:p, check=0){
	Z[i1+1:n,1+i1] = Y[1:n-i1,]
}
parfor(i2 in 1:P, check=0){
	Z[s*i2+1:n,1+p+i2] = Y[1:n-s*i2,]
}
parfor(i5 in 1:q, check=0){
	Z[i5+1:n,1+P+p+i5] = Y[1:n-i5,]
}
parfor(i6 in 1:Q, check=0){
	Z[s*i6+1:n,1+P+p+q+i6] = Y[1:n-s*i6,]
}

one = Rand(rows=1, cols=1, min=1, max=1)

simplex = Rand(rows=totcols-1, cols=totcols, min=0, max=0)
for(i in 2:ncol(simplex)){
	simplex[i-1,i] = 0.1
}

num_func_invoc = 0

objvals = Rand(rows=1, cols=ncol(simplex), min=0, max=0)
parfor(i3 in 1:ncol(simplex)){
	arima_css_obj_val = arima_css(simplex[,i3], Z, p, P, q, Q, s)
	objvals[1,i3] = arima_css_obj_val
}
num_func_invoc = num_func_invoc + ncol(simplex)

tol = 1.5 * 10^(-8) * castAsScalar(objvals[1,1])

continue = 1
while(continue == 1 & num_func_invoc <= max_func_invoc) {
	best_index = 1
	worst_index = 1
	for(i in 2:ncol(objvals)){
		this = castAsScalar(objvals[1,i])
		that = castAsScalar(objvals[1,best_index])
  		if(that > this){
    		best_index = i
  		}
  		that = castAsScalar(objvals[1,worst_index])
  		if(that < this){
    		worst_index = i
  		}
	}
	
	best_obj_val = castAsScalar(objvals[1,best_index])
	worst_obj_val = castAsScalar(objvals[1,worst_index])
	if(worst_obj_val <= best_obj_val + tol){
		continue = 0
	}
	
	print("#Function calls::" + num_func_invoc + " OBJ: " + best_obj_val)
	
	c = (rowSums(simplex) - simplex[,worst_index])/(nrow(simplex))
	
	x_r = 2*c - simplex[,worst_index]
	obj_x_r = arima_css(x_r, Z, p, P, q, Q, s)
	num_func_invoc = num_func_invoc + 1
	
	if(obj_x_r < best_obj_val){
		x_e = 2*x_r - c
		obj_x_e = arima_css(x_e, Z, p, P, q, Q, s)
		num_func_invoc = num_func_invoc + 1
		
		if(obj_x_r <= obj_x_e){
			simplex[,worst_index] = x_r
			objvals[1,worst_index] = obj_x_r
		}else{
			simplex[,worst_index] = x_e
			objvals[1,worst_index] = obj_x_e
		}
	}else{
		if(obj_x_r < worst_obj_val){
			simplex[,worst_index] = x_r
			objvals[1,worst_index] = obj_x_r
		}
		
		x_c_in = (simplex[,worst_index] + c)/2
		obj_x_c_in = arima_css(x_c_in, Z, p, P, q, Q, s)
		num_func_invoc = num_func_invoc + 1
		
		if(obj_x_c_in < castAsScalar(objvals[1,worst_index])){
			simplex[,worst_index] = x_c_in
			objvals[1,worst_index] = obj_x_c_in
		}else{
			if(obj_x_r >= worst_obj_val){
				best_point = simplex[,best_index]
				parfor(i4 in 1:ncol(simplex)){
					if(i4 != best_index){
						simplex[,i4] = (simplex[,i4] + best_point)/2
						tmp = arima_css(simplex[,i4], Z, p, P, q, Q, s)
						objvals[1,i4] = tmp*one
					}
				}
				num_func_invoc = num_func_invoc + ncol(simplex) - 1
			}
		}
	}
}

best_point = simplex[,best_index]
if(include_mean == 1){
	tmp = Rand(rows=totcols+1, cols=1, min=0, max=0)
	tmp[1:nrow(best_point),1] = best_point
	tmp[nrow(tmp),1] = mu
	best_point = tmp
}

write(best_point, $11, format="text")
