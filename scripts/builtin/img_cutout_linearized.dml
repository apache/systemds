#-------------------------------------------------------------
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
#-------------------------------------------------------------

# Image Cutout function replaces a rectangular section of an image with a constant value.
#
# INPUT:
# ---------------------------------------------------------------------------------------------
# img_in      Input images as linearized 2D matrix with top left corner at [1, 1]
# x           Column index of the top left corner of the rectangle (starting at 1)
# y           Row index of the top left corner of the rectangle (starting at 1)
# width       Width of the rectangle (must be positive)
# height      Height of the rectangle (must be positive)
# fill_value   The value to set for the rectangle
# s_cols      Width of a single image
# s_rows      Height of a single image
# ---------------------------------------------------------------------------------------------
#
# OUTPUT:
# ------------------------------------------------------------------------------------------
# img_out     Output images as linearized 2D matrix with top left corner at [1, 1]
# ------------------------------------------------------------------------------------------

m_img_cutout_linearized = function(Matrix[Double] img_in, Integer x, Integer y, Integer width, Integer height, 
  Double fill_value, Integer s_cols, Integer s_rows) return (Matrix[Double] img_out) {
  rows = nrow(img_in) 
  cols = ncol(img_in) 

  if (width < 1 | height < 1) {
    print("Invalid width or height. Returning input")
    img_out = img_in
  } else {

    start_x = max(1, x)
    start_y = max(1, y)

    end_x = start_x + width - 1
    end_x = min(s_cols, end_x)

    end_y = start_y + height - 1
    end_y = min(s_rows, end_y)

    img_out = img_in

    # Iterate through each row of the rectangular region
    for (i in start_y: end_y){
      start_idx = (i-1) * s_cols + start_x
      end_idx = (i-1) * s_cols + end_x

      img_out[, start_idx:end_idx] = matrix(fill_value, rows=rows, cols=(end_x-start_x+1))
    }
  }
}
