#-------------------------------------------------------------
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
#-------------------------------------------------------------

/*
 * Logarithm of the hyperbolic cosine of the prediction error.
 */

forward = function(matrix[double] pred, matrix[double] y)
    return (double loss) {
  /*
   * Computes the forward pass for an logcos loss function.  The inputs
   * consist of N examples, each with M dimensions to predict.
   *
   *   ```
   *   L = sum( log(cosh(p_i - y_i)) for i=1 to N) / N
   *   ```
   *
   * Inputs:
   *  - pred: Predictions, of shape (N, M).
   *  - y: Targets, of shape (N, M).
   *
   * Outputs:
   *  - loss: Average loss.
   */
  N = nrow(y)
  losses = log(cosh(pred - y))
  loss = sum(losses) / N
}

backward = function(matrix[double] pred, matrix[double] y)
    return (matrix[double] dpred) {
  /*
   * Computes the backward pass for an logcosh loss function. The inputs
   * consist of N examples, each with M dimensions to predict.
   *
   * Inputs:
   *  - pred: Predictions, of shape (N, M).
   *  - y: Targets, of shape (N, M).
   *
   * Outputs:
   *  - dpred: Gradient wrt `pred`, of shape (N, M).
   */

  N = nrow(y)
  # the derivative of log(cosh(x)) is tanh(x)
  dpred = tanh(pred-y) / N
}
