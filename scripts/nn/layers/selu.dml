#-------------------------------------------------------------
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
#-------------------------------------------------------------

/*
 * SeLU (Scaled Exponential Linear Unit) nonlinearity layer.
 */

forward = function(matrix[double] X)
    return (matrix[double] out) {
  /*
   * Computes the forward pass for a SeLU nonlinearity layer.
   *
   *   selu(x) = lambda * ( x                if x > 0
   *                 Î± * (exp(x) - 1)  if x <= 0 )
   *
   * Inputs:
   *  - X: Inputs, of shape (any, any).
   *
   * Outputs:
   *  - out: Outputs, of same shape as `X`.
   */
  alpha = 1.6732632423543772
  lambda = 1.0507009873554805

  out = (X > 0) * (lambda * X) +
        (X <= 0) * (lambda * alpha * (exp(X) - 1))
}

backward = function(matrix[double] dout, matrix[double] X)
    return (matrix[double] dX) {
  /*
   * Computes the backward pass for a SeLU nonlinearity layer.
   *
   * Inputs:
   *  - dout: Gradient wrt `out` from upstream, of same shape as `X`.
   *  - X: Inputs, of shape (any, any).
   *
   * Outputs:
   *  - dX: Gradient wrt `X`, of same shape as `X`.
   */
  alpha = 1.6732632423543772
  lambda = 1.0507009873554805

  dselu = (X > 0) * lambda +
          (X <= 0) * (lambda * alpha * exp(X))

  dX = dselu * dout
}
