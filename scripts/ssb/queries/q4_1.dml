/* DML-script implementing the ssb query Q4.2 in SystemDS.
**input_dir="/scripts/ssb/data"

* Run with docker:
docker run -it --rm -v $PWD:/scripts/ apache/systemds:nightly -f /scripts/queries/q4_2.dml -nvargs input_dir="/scripts/data/"

SELECT
    d_year,
    c_nation,
    SUM(lo_revenue - lo_supplycost) AS PROFIT
FROM date, customer, supplier, part, lineorder -- dates
WHERE
    lo_custkey = c_custkey
    AND lo_suppkey = s_suppkey
    AND lo_partkey = p_partkey
    AND lo_orderdate = d_datekey
    AND c_region = 'AMERICA'
    AND s_region = 'AMERICA'
    AND (
        p_mfgr = 'MFGR#1'
        OR p_mfgr = 'MFGR#2'
    )
GROUP BY d_year, c_nation
ORDER BY d_year, c_nation;

*Please run the original SQL query (eg. in Postgres) 
to verify the correctness of DML version.
-> First tests: Works on the dataset with scale factor 0.1.
-> Sorting does not work.

*Based on older implementations.
https://github.com/ghafek/systemds/blob/feature/ssb-benchmark/scripts/ssb/queries/q1_1.dml
*Especially:
https://github.com/ghafek/systemds/blob/feature/ssb-benchmark/scripts/ssb/queries/q4_3.dml
In comparison to older version the join method was changed
from sort-merge to hash2 to improve the performance.

Input parameters:
input_dir - Path to input directory containing the table files (e.g., ./data)
*/

# Call ra-modules with ra-functions.
source("./scripts/builtin/raSelection.dml") as raSel
source("./scripts/builtin/raJoin.dml") as raJoin
source("./scripts/builtin/raGroupby.dml") as raGrp

# Set input parameters.
input_dir = ifdef($input_dir, "./data");
print("Loading tables from directory: " + input_dir);

# Read and load input CSV files.  
lineorder_csv = read(input_dir + "/lineorder.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
cust_csv = read(input_dir + "/customer.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
date_csv = read(input_dir + "/date.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
part_csv = read(input_dir + "/part.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
supp_csv = read(input_dir + "/supplier.tbl", data_type="frame", format="csv", header=FALSE, sep="|");

general_spec = "{ \"ids\": false, \"recode\": [\"C1\"] }";

# -- Data preparation --

# Extract only the necessary columns from tables.
# Extracted: COL-3 | COL-4 | COL-5 | COL-6 | COL-13 | COL-14
# => LO_CUSTKEY | LO_PARTKEY | LO_SUPPKEY | LO_ORDERDATE | 
# LO_REVENUE | LO_SUPPLYCOST
lineorder_csv_min = cbind(lineorder_csv[, 3], lineorder_csv[, 4], lineorder_csv[, 5], lineorder_csv[, 6], lineorder_csv[, 13], lineorder_csv[, 14]);
lineorder_matrix_min = as.matrix(lineorder_csv_min);

# Extracted: COL-1 | COL-5
# => D_DATEKEY | D_YEAR
date_csv_min = cbind(date_csv[, 1], date_csv[, 5]);
date_matrix_min = as.matrix(date_csv_min);

# -- Filter tables over string values.

# Prepare PART table on-the-fly encodings
# Extracted: COL-1 | COL-3
# P_PARTKEY | P_MFGR

# Build filtered PART table (P_MFGR = 'MFGR#1' OR P_MFGR = 'MFGR#2'), keeping key
part_filt = matrix(0, rows=0, cols=1);
for (i in 1:nrow(part_csv)) {
    p_elem = as.scalar(part_csv[i,3])
    if ( p_elem == "MFGR#1" | p_elem == "MFGR#2" ) {
        key_val = as.double(as.scalar(part_csv[i,1]));
        part_filt = rbind(part_filt, matrix(key_val, rows=1, cols=1));
    }
}
if (nrow(part_filt) == 0) {
    part_filt = matrix(0, rows=1, cols=1);
}

# Extracted: COL-1 | COL-6
# S_SUPPKEY | S_REGION
# Build filtered SUPPLIER table (S_NATION = 'AMERICA')
supp_filt = matrix(0, rows=0, cols=1);
for (i in 1:nrow(supp_csv)) {
    if (as.scalar(supp_csv[i,6]) == "AMERICA") {
        key_val = as.double(as.scalar(supp_csv[i,1]));
        supp_filt = rbind(supp_filt, matrix(key_val, rows=1, cols=1));
    }
}
if (nrow(supp_filt) == 0) {
    supp_filt = matrix(0, rows=1, cols=1);
}

# Prepare CUSTOMER table on-the-fly encodings
# Extracted: COL-1 | COL-5 | COL-6
# C_CUSTKEY | C_NATION | C_REGION
# (only need C_NATION encoding, filter by C_REGION string)
[cust_nat_enc_f, cust_nat_meta] = transformencode(target=cust_csv[,5], spec=general_spec);

# Build filtered CUSTOMER table (C_NATION = 'AMERICA')
cust_filt_keys = matrix(0, rows=0, cols=1);
cust_filt_nat = matrix(0, rows=0, cols=1);
for (i in 1:nrow(cust_csv)) {
    if (as.scalar(cust_csv[i,6]) == "AMERICA") {
        key_val = as.double(as.scalar(cust_csv[i,1]));
        nat_code = as.double(as.scalar(cust_nat_enc_f[i,1]));
        cust_filt_keys = rbind(cust_filt_keys, matrix(key_val, rows=1, cols=1));
        cust_filt_nat = rbind(cust_filt_nat, matrix(nat_code, rows=1, cols=1));
    }
}
if (nrow(cust_filt_keys) == 0) {
    cust_filt_keys = matrix(0, rows=1, cols=1);
    cust_filt_nat = matrix(0, rows=1, cols=1);
}
cust_filt = cbind(cust_filt_keys,cust_filt_nat);

#print("LO,DATE,CUST,PART,SUPP")
#print(toString(lineorder_matrix_min[1,]))
#print(toString(date_matrix_min[1,]))
#print(toString(cust_filt[1,]))
#print(toString(part_filt[1,]))
#print(toString(supp_filt[1,]))


# -- JOIN TABLES WITH RA-JOIN FUNCTION --

# Join LINEORDER table with CUST, SUPPLIER, PART, DATE tables (star schema)
# Join order does matter! 
# WHERE LO_CUSTKEY = C_CUSTKEY
lo_cust = raJoin::m_raJoin(A=cust_filt, colA=1, B=lineorder_matrix_min, colB=1, method="hash2");
# WHERE LO_SUPPKEY = S_SUPPKEY
lo_cust_supp = raJoin::m_raJoin(A=supp_filt, colA=1, B=lo_cust, colB=5, method="hash2");
# WHERE LO_PARTKEY = P_PARTKEY
lo_cust_supp_part = raJoin::m_raJoin(A=part_filt, colA=1, B=lo_cust_supp, colB=5, method="hash2");
# WHERE LO_ORDERDATE = D_DATEKEY
# (D_DATEKEY | D_YEAR) | (P_PARTKEY | S_SUPPKEY | C_CUSTKEY | C_NATION |
# LO_CUSTKEY | LO_PARTKEY | LO_SUPPKEY | LO_ORDERDATE | LO_REVENUE | LO_SUPPLYCOST)
joined_matrix = raJoin::m_raJoin(A=date_matrix_min, colA=1, B=lo_cust_supp_part, colB=8, method="hash2");

#print(toString(joined_matrix[1,]))

# -- Group-By and Aggregation (SUM)--

# Group-By
c_nat = joined_matrix[,6]
d_year = joined_matrix[,2]
lo_revenue = joined_matrix[,11]
lo_supplycost = joined_matrix[,12]
profit = lo_revenue - lo_supplycost;

# CALCULATING COMBINATION KEY WITH PRIORITY:1 D_YEAR, 2 S_NATION
max_d_year = max(d_year);
max_c_nat= max(c_nat);

d_year_scale_f = ceil(max_d_year) + 1;
c_nat_scale_f = ceil(max_c_nat) + 1;

combined_key = d_year * c_nat_scale_f + c_nat;

group_input = cbind(profit, combined_key)

agg_result = raGrp::m_raGroupby(X=group_input, col=2, method="nested-loop");
#print(toString(agg_result[1,]));

# Aggregation (SUM)
key = agg_result[, 1];
profit = rowSums(agg_result[, 2:ncol(agg_result)]);

# EXTRACTING D_YEAR, C_NATION
d_year = round(floor(key / (c_nat_scale_f)));
c_nat = round(floor((key %% (c_nat_scale_f))));

result = cbind(d_year, c_nat, profit, key);

# -- Sorting -- -- Sorting int columns works, but strings do not.
# ORDER BY D_YEAR, C_NATION ASC
result_ordered = order(target=result, by=4, decreasing=FALSE, index.return=FALSE);

c_nat_dec = transformdecode(target=result_ordered[,2], spec=general_spec, meta=cust_nat_meta);

res = cbind(as.frame(result_ordered[,1]), c_nat_dec, as.frame(result_ordered[,3])) ;

# Print result
print("d_year | c_nation | PROFIT")
print(res)

print("\nQ4.1 finished.\n");