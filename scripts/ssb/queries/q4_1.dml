# DML-script implementing the ssb query Q4.1 in SystemDS.


# -- SOURCING THE RA-FUNCTIONS --
source("./scripts/builtin/raSelection.dml") as raSel
source("./scripts/builtin/raJoin.dml") as raJoin
source("./scripts/builtin/raGroupby.dml") as raGrp


# -- READING INPUT FILES --
# CSV TABLES
date_csv = read("./data/date.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
lineorder_csv = read("./data/lineorder3.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
part_csv = read("./data/part.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
supplier_csv = read("./data/supplier.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
customer_csv = read("./data/customer.tbl", data_type="frame", format="csv", header=FALSE, sep="|");

# META DATA
part_meta = read("./data/meta/part_meta.csv", data_type="frame", format="csv", header=FALSE, sep="|");
customer_meta = read("./data/meta/customer_meta.csv", data_type="frame", format="csv", header=FALSE, sep="|");
supplier_meta = read("./data/meta/supplier_meta.csv", data_type="frame", format="csv", header=FALSE, sep="|");


# -- PREPARING --
# EXTRACTING MINIMAL DATE DATA TO OPTIMIZE RUNTIME => COL-1 : DATE-KEY | COL-5 : D_YEAR
date_csv_min = cbind(date_csv[, 1], date_csv[, 5]);
date_matrix_min = as.matrix(date_csv_min);

# EXTRACTING MINIMAL LINEORDER DATA TO OPTIMIZE RUNTIME => COL-3 : LO_CUSTKEY | COL-4 : LO_PARTKEY |
# COL-5 : LO_SUPPKEY | COL-6 : LO_ORDERDATE | COL-13 : LO_REVENUE | COL-14 : LO_SUPPLYCOST
lineorder_csv_min = cbind(lineorder_csv[, 3], lineorder_csv[, 4], lineorder_csv[, 5], lineorder_csv[, 6], lineorder_csv[, 13], lineorder_csv[, 14]);
lineorder_matrix_min = as.matrix(lineorder_csv_min);

# EXTRACTING MINIMAL PART DATA TO OPTIMIZE RUNTIME => COL-1 : PART-KEY | COL-3 : P_MFGR
part_csv_min = cbind(part_csv[, 1], part_csv[, 3]);
part_enc_meta = cbind(part_meta[, 1], part_meta[, 3]);
part_enc_spec = "{ \"recode\": [\"C2\"] }";
part_matrix_min = transformapply(target=part_csv_min, meta=part_enc_meta, spec=part_enc_spec);

# EXTRACTING MINIMAL CUSTOMER DATA TO OPTIMIZE RUNTIME => COL-1 : CUSTOMER-KEY | COL-5 : C_NATION |
# COL-6 : C_REGION
cust_csv_min = cbind(customer_csv[, 1], customer_csv[, 5], customer_csv[, 6]);
cust_enc_meta = cbind(customer_meta[, 1], customer_meta[, 5], customer_meta[, 6]);
cust_enc_spec = "{ \"recode\": [\"C2\", \"C3\"] }";
cust_matrix_min = transformapply(target=cust_csv_min, meta=cust_enc_meta, spec=cust_enc_spec);

# EXTRACTING MINIMAL SUPPLIERS DATA TO OPTIMIZE RUNTIME => COL-1 : SUP-KEY | COL-6 : S_REGION
sup_csv_min = cbind(supplier_csv[, 1], supplier_csv[, 6]);
sup_enc_meta = cbind(supplier_meta[, 1], supplier_meta[, 6]);
sup_enc_spec = "{ \"recode\": [\"C2\"] }";
sup_matrix_min = transformapply(target=sup_csv_min, meta=sup_enc_meta, spec=sup_enc_spec);


# -- FILTERING THE DATA WITH RA-SELECTION FUNCTION --
#  C_REGION = 'AMERICA' : 3 (MAPPED VALUE IN CUSTOMER_META.CSV)
c_reg_filt = raSel::m_raSelection(cust_matrix_min, col=3, op="==", val=3);

# S_REGION = 'AMERICA' : 1 (MAPPED VALUE IN SUPLLIER_META.CSV)
s_reg_filt = raSel::m_raSelection(sup_matrix_min, col=2, op="==", val=1);

# P_MFGR = 'MFGR#1' : 1 OR 'MFGR#2' : 4 (MAPPED VALUES IN PART_META.CSV)
p_mfgr_filt_1 = raSel::m_raSelection(part_matrix_min, col=2, op="==", val=1);
p_mfgr_filt_2 = raSel::m_raSelection(part_matrix_min, col=2, op="==", val=4);
p_mfgr_filt = rbind(p_mfgr_filt_1, p_mfgr_filt_2);


# -- JOIN TABLES WITH RA-JOIN FUNCTION --
# JOINING MINIMIZED LINEORDER TABLE WITH FILTERED CUSTOMER TABLE WHERE LO_CUSTKEY = C_CUSTKEY
lo_cust = raJoin::m_raJoin(A=lineorder_matrix_min, colA=1, B=c_reg_filt, colB=1, method="sort-merge");

# JOIN: ⨝ SUPPLIER WHERE LO_SUPPKEY = S_SUPPKEY
lo_cust_sup = raJoin::m_raJoin(A=lo_cust, colA=3, B=s_reg_filt, colB=1, method="sort-merge");

# JOIN: ⨝ PART WHERE LO_PARTKEY = P_PARTKEY
lo_cust_sup_part = raJoin::m_raJoin(A=lo_cust_sup, colA=2, B=p_mfgr_filt, colB=1, method="sort-merge");

# JOIN: ⨝ DATE WHERE LO_ORDERDATE = D_DATEKEY
joined_matrix = raJoin::m_raJoin(A=lo_cust_sup_part, colA=4, B=date_matrix_min, colB=1, method="sort-merge");
#print(nrow(joined_matrix));


# -- GROUP-BY & AGGREGATION --
# LO_REVENUE : COLUMN 5 OF LINEORDER-MIN-MATRIX
lo_revenue = joined_matrix[, 5];
# LO_SUPPLYCOST : COLUMN 6 OF LINEORDER-MIN-MATRIX
lo_supplycost = joined_matrix[, 6];
# D_YEAR : COLUMN 2 OF DATE-MIN-MATRIX
d_year = joined_matrix[,(ncol(lineorder_matrix_min) + ncol(cust_matrix_min) + ncol(sup_matrix_min) + ncol(part_matrix_min) + 2)];
# C_NATION : COLUMN 2 OF CUSTOMER-MIN-MATRIX
c_nation = joined_matrix[,(ncol(lineorder_matrix_min) + 2)];

profit = lo_revenue - lo_supplycost;

# CALCULATING COMBINATION KEY WITH PRIORITY: D_YEAR, C_NATION
max_c_nation= max(c_nation);
max_d_year = max(d_year);

c_nation_scale_f = ceil(max_c_nation) + 1;
d_year_scale_f = ceil(max_d_year) + 1;

combined_key = c_nation * d_year_scale_f + d_year;

group_input = cbind(profit, combined_key);
agg_result = raGrp::m_raGroupby(X=group_input, col=2, method="nested-loop");

key = agg_result[, 1];
profit = rowSums(agg_result[, 2:ncol(agg_result)]);

# EXTRACTING D_YEAR, C_NATION
d_year = round(key %% d_year_scale_f);
c_nation = round(floor(key / d_year_scale_f));

result = cbind(d_year, c_nation, profit);


# -- SORTING --
# PRIORITY 1 D_YEAR , 2 C_NATION
result_ordered = order(target=result, by=2, decreasing=FALSE, index.return=FALSE);
result_ordered = order(target=result_ordered, by=1, decreasing=FALSE, index.return=FALSE);
print(result_ordered);


# -- DECODING C_NATION --
cust_dec_meta = customer_meta[, 5];
cust_dec_spec = "{ \"recode\": [\"C1\"] }";

c_nat_dec = transformdecode(target=result_ordered[, 2], spec=cust_dec_spec, meta=cust_dec_meta);

res = cbind(as.frame(result_ordered[, 1]), c_nat_dec, as.frame(result_ordered[, 3]));

print(res);
print("Q4.1 finished");

