/* DML-script implementing the ssb query Q4.2 in SystemDS.
SELECT
    d_year,
    s_nation,
    p_category,
    SUM(lo_revenue - lo_supplycost) AS PROFIT
FROM dates, customer, supplier, part, lineorder
WHERE
    lo_custkey = c_custkey
    AND lo_suppkey = s_suppkey
    AND lo_partkey = p_partkey
    AND lo_orderdate = d_datekey
    AND c_region = 'AMERICA'
    AND s_region = 'AMERICA'
    AND (
        d_year = 1997
        OR d_year = 1998
    )
    AND (
        p_mfgr = 'MFGR#1'
        OR p_mfgr = 'MFGR#2'
    )
GROUP BY d_year, s_nation, p_category
ORDER BY d_year, s_nation, p_category;
*/

# -- SOURCING THE RA-FUNCTIONS --
source("./scripts/builtin/raSelection.dml") as raSel
source("./scripts/builtin/raJoin.dml") as raJoin
source("./scripts/builtin/raGroupby.dml") as raGrp


# -- READING INPUT FILES --
# CSV TABLES
date_csv = read("./data/date.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
lineorder_csv = read("./data/lineorder3.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
part_csv = read("./data/part.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
supplier_csv = read("./data/supplier.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
customer_csv = read("./data/customer.tbl", data_type="frame", format="csv", header=FALSE, sep="|");

# META DATA
part_meta = read("./data/meta/part_meta.csv", data_type="frame", format="csv", header=FALSE, sep="|");
customer_meta = read("./data/meta/customer_meta.csv", data_type="frame", format="csv", header=FALSE, sep="|");
supplier_meta = read("./data/meta/supplier_meta.csv", data_type="frame", format="csv", header=FALSE, sep="|");


# -- PREPARING --
# EXTRACTING MINIMAL DATE DATA TO OPTIMIZE RUNTIME => COL-1 : DATE-KEY | COL-5 : D_YEAR
date_csv_min = cbind(date_csv[, 1], date_csv[, 5]);
date_matrix_min = as.matrix(date_csv_min);

# EXTRACTING MINIMAL LINEORDER DATA TO OPTIMIZE RUNTIME => COL-3 : LO_CUSTKEY | COL-4 : LO_PARTKEY |
# COL-5 : LO_SUPPKEY | COL-6 : LO_ORDERDATE | COL-13 : LO_REVENUE | COL-14 : LO_SUPPLYCOST
lineorder_csv_min = cbind(lineorder_csv[, 3], lineorder_csv[, 4], lineorder_csv[, 5], lineorder_csv[, 6], lineorder_csv[, 13], lineorder_csv[, 14]);
lineorder_matrix_min = as.matrix(lineorder_csv_min);

# EXTRACTING MINIMAL PART DATA TO OPTIMIZE RUNTIME => COL-1 : PART-KEY | COL-3 : P_MFGR | 
# COL-4 : P_CATEGORY
part_csv_min = cbind(part_csv[, 1], part_csv[, 3], part_csv[, 4]);
part_enc_meta = cbind(part_meta[, 1], part_meta[, 3], part_meta[, 4]);
part_enc_spec = "{ \"recode\": [\"C2\", \"C3\"] }";
part_matrix_min = transformapply(target=part_csv_min, meta=part_enc_meta, spec=part_enc_spec);

# EXTRACTING MINIMAL CUSTOMER DATA TO OPTIMIZE RUNTIME => COL-1 : CUSTOMER-KEY | COL-6 : C_REGION
cust_csv_min = cbind(customer_csv[, 1], customer_csv[, 6]);
cust_enc_meta = cbind(customer_meta[, 1], customer_meta[, 6]);
cust_enc_spec = "{ \"recode\": [\"C2\"] }";
cust_matrix_min = transformapply(target=cust_csv_min, meta=cust_enc_meta, spec=cust_enc_spec);

# EXTRACTING MINIMAL SUPPLIERS DATA TO OPTIMIZE RUNTIME => COL-1 : SUP-KEY | COL-5 : S_NATION |
# COL-6 : S_REGION
sup_csv_min = cbind(supplier_csv[, 1], supplier_csv[, 5], supplier_csv[, 6]);
sup_enc_meta = cbind(supplier_meta[, 1], supplier_meta[, 5], supplier_meta[, 6]);
sup_enc_spec = "{ \"recode\": [\"C2\", \"C3\"] }";
sup_matrix_min = transformapply(target=sup_csv_min, meta=sup_enc_meta, spec=sup_enc_spec);


# -- FILTERING THE DATA WITH RA-SELECTION FUNCTION --
#  C_REGION = 'AMERICA' : 3 (MAPPED VALUE IN CUSTOMER_META.CSV)
c_reg_filt = raSel::m_raSelection(cust_matrix_min, col=2, op="==", val=3);

# S_REGION = 'AMERICA' : 1 (MAPPED VALUE IN SUPLLIER_META.CSV)
s_reg_filt = raSel::m_raSelection(sup_matrix_min, col=3, op="==", val=1);

# P_MFGR = 'MFGR#1' : 1 OR 'MFGR#2' : 4 (MAPPED VALUES IN PART_META.CSV)
p_mfgr_filt_1 = raSel::m_raSelection(part_matrix_min, col=2, op="==", val=1);
p_mfgr_filt_2 = raSel::m_raSelection(part_matrix_min, col=2, op="==", val=4);
p_mfgr_filt = rbind(p_mfgr_filt_1, p_mfgr_filt_2);

# D_YEAR = 1997 OR 1998
d_year_filt_1 = raSel::m_raSelection(date_matrix_min, col=2, op="==", val=1997);
d_year_filt_2 = raSel::m_raSelection(date_matrix_min, col=2, op="==", val=1998);
d_year_filt = rbind(d_year_filt_1, d_year_filt_2);


# -- JOIN TABLES WITH RA-JOIN FUNCTION --
# JOINING MINIMIZED LINEORDER TABLE WITH FILTERED CUSTOMER TABLE WHERE LO_CUSTKEY = C_CUSTKEY
lo_cust = raJoin::m_raJoin(A=lineorder_matrix_min, colA=1, B=c_reg_filt, colB=1, method="sort-merge");

# JOIN: ⨝ SUPPLIER WHERE LO_SUPPKEY = S_SUPPKEY
lo_cust_sup = raJoin::m_raJoin(A=lo_cust, colA=3, B=s_reg_filt, colB=1, method="sort-merge");

# JOIN: ⨝ PART WHERE LO_PARTKEY = P_PARTKEY
lo_cust_sup_part = raJoin::m_raJoin(A=lo_cust_sup, colA=2, B=p_mfgr_filt, colB=1, method="sort-merge");

# JOIN: ⨝ DATE WHERE LO_ORDERDATE = D_DATEKEY
joined_matrix = raJoin::m_raJoin(A=lo_cust_sup_part, colA=4, B=d_year_filt, colB=1, method="sort-merge");
#print(nrow(joined_matrix));


# -- GROUP-BY & AGGREGATION --
# LO_REVENUE : COLUMN 5 OF LINEORDER-MIN-MATRIX
lo_revenue = joined_matrix[, 5];
# LO_SUPPLYCOST : COLUMN 6 OF LINEORDER-MIN-MATRIX
lo_supplycost = joined_matrix[, 6];
# D_YEAR : COLUMN 2 OF DATE-MIN-MATRIX
d_year = joined_matrix[,(ncol(lineorder_matrix_min) + ncol(cust_matrix_min) + ncol(sup_matrix_min) + ncol(part_matrix_min) + 2)];
# S_NATION : COLUMN 2 OF SUPPLIER-MIN-MATRIX
s_nation = joined_matrix[,(ncol(lineorder_matrix_min) + ncol(cust_matrix_min) + 2)];
# P_CATEGORY : COLUMN 3 OF PART-MIN-MATRIX
p_category = joined_matrix[,(ncol(lineorder_matrix_min) + ncol(cust_matrix_min) + ncol(sup_matrix_min) + 3)];

profit = lo_revenue - lo_supplycost;

# CALCULATING COMBINATION KEY WITH PRIORITY: D_YEAR, S_NATION, P_CATEGORY
max_s_nation = max(s_nation);
max_p_category = max(p_category);
max_d_year = max(d_year);

s_nation_scale_f = ceil(max_s_nation) + 1;
p_category_scale_f = ceil(max_p_category) + 1;
d_year_scale_f = ceil(max_d_year) + 1;

combined_key = d_year * s_nation_scale_f * p_category_scale_f + s_nation * p_category_scale_f + p_category;

group_input = cbind(profit, combined_key);
agg_result = raGrp::m_raGroupby(X=group_input, col=2, method="nested-loop");

key = agg_result[, 1];
profit = rowSums(agg_result[, 2:ncol(agg_result)]);

# EXTRACTING D_YEAR, S_NATION, P_CATEGORY
d_year = round(floor(key / (s_nation_scale_f * p_category_scale_f)));
s_nation = round(floor((key %% (s_nation_scale_f * p_category_scale_f)) / p_category_scale_f));
p_category = round(key %% p_category_scale_f);

result = cbind(d_year, s_nation, p_category, profit, key);


# -- SORTING --
# PRIORITY 1 D_YEAR , 2 S_NATION, 3 P_CATEGORY
result_ordered = order(target=result, by=5, decreasing=FALSE, index.return=FALSE);
print(result_ordered);


# -- DECODING S_NATION & P_CATEGORY --
sup_dec_meta = supplier_meta[, 5];
sup_dec_spec = "{ \"recode\": [\"C1\"] }";

part_dec_meta = part_meta[, 4];
part_dec_spec = "{ \"recode\": [\"C1\"] }";

s_nat_dec = transformdecode(target=result_ordered[, 2], spec=sup_dec_spec, meta=sup_dec_meta);
p_cat_dec = transformdecode(target=result_ordered[, 3], spec=part_dec_spec, meta=part_dec_meta);

res = cbind(as.frame(result_ordered[, 1]), s_nat_dec, p_cat_dec, as.frame(result_ordered[, 4]));

print(res);
print("Q4.2 finished");

