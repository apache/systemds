#-------------------------------------------------------------
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
# 
#   http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
#-------------------------------------------------------------

/*
 * Log loss function.
 *
 * L_i = -y_i*log(pred_i) - (1-y_i)*log(1-pred_i), where y_i is a
 *  binary target, and pred_i is a probability of y=1. 
 * L = (1/N) sum(L_i) for i=1 to N, where N is the number of examples.
 */
forward = function(matrix[double] pred, matrix[double] y) 
    return (double loss) {
  /*
   * Computes the forward pass for a log loss function.
   *
   * This can be interpreted as the negative log-likelihood assuming
   * a Bernoulli distribution.
   *
   * Inputs:
   *  - pred: Prediction matrix, of shape (N, 1).  Predictions should
   *      be probabilities that y=1.
   *  - y: Target matrix, of shape (N, 1).  Targets should be binary
   *      in the set {0,1}.
   *
   * Outputs:
   *  - loss: Scalar loss, of shape (1).
   */
  N = nrow(y)
  losses = -y * log(pred) - (1-y) * log(1-pred)
  loss = sum(losses) / N
}

backward = function(matrix[double] pred, matrix[double] y) 
    return (matrix[double] dpred) {
  /*
   * Computes the backward pass for a log loss function.
   *
   * Inputs:
   *  - pred: Prediction matrix, of shape (N, 1).  Predictions should
   *      be probabilities that y=1.
   *  - y: Target matrix, of shape (N, 1).  Targets should be binary
   *      in the set {0,1}.
   *
   * Outputs:
   *  - dpred: Gradient wrt pred, of shape (N, 1).
   */
  N = nrow(y)
  dpred = (1/N) * (pred-y) / (pred * (1-pred))
}

