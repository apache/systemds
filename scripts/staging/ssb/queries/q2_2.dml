#-------------------------------------------------------------
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
#-------------------------------------------------------------


/* DML-script implementing the ssb query Q2.2 in SystemDS.
**input_dir="/scripts/ssb/data"

* Run with docker:
docker run -it --rm -v $PWD:/scripts/ apache/systemds:nightly -f /scripts/queries/q2_2.dml -nvargs input_dir="/scripts/data/"

SELECT SUM(lo_revenue), d_year, p_brand
FROM lineorder, date, part, supplier --dates
WHERE
    lo_orderdate = d_datekey
    AND lo_partkey = p_partkey
    AND lo_suppkey = s_suppkey
    AND p_brand BETWEEN 'MFGR#2221' AND 'MFGR#2228'
    AND s_region = 'ASIA'
GROUP BY d_year, p_brand
ORDER BY d_year, p_brand;

*Please run the original SQL query (eg. in Postgres) 
to verify the correctness of DML version.
-> First tests: Works on the dataset with scale factor 0.1.
-> Sorting does not work.

*Based on older implementations.
https://github.com/ghafek/systemds/blob/feature/ssb-benchmark/scripts/ssb/queries/q1_1.dml
*Especially:
https://github.com/ghafek/systemds/blob/feature/ssb-benchmark/scripts/ssb/queries/q4_3.dml
In comparison to older version the join method was changed
from sort-merge to hash2 to improve the performance.

Input parameters:
input_dir - Path to input directory containing the table files (e.g., ./data)
*/

# Call ra-modules with ra-functions.
source("./scripts/builtin/raSelection.dml") as raSel
source("./scripts/builtin/raJoin.dml") as raJoin
source("./scripts/builtin/raGroupby.dml") as raGrp

# Set input parameters.
input_dir = ifdef($input_dir, "./data");
print("Loading tables from directory: " + input_dir);

# Read and load input CSV files from lineorder, date, part, supplier.  
lineorder_csv = read(input_dir + "/lineorder.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
date_csv = read(input_dir + "/date.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
part_csv = read(input_dir + "/part.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
supp_csv = read(input_dir + "/supplier.tbl", data_type="frame", format="csv", header=FALSE, sep="|");

# General variables.
general_spec = "{ \"ids\": false, \"recode\": [\"C1\"] }";
hasRows = 1; # If hasRows = 0, the result table is empty.

# -- Data preparation --

# Extract only the necessary columns from tables.
# Extracted: COL-4 | COL-5 | COL-6 | COL_COL-13
# => LO_PARTKEY | LO_SUPPKEY | LO_ORDERDATE | LO_REVENUE
lineorder_csv_min = cbind(lineorder_csv[, 4], lineorder_csv[, 5], lineorder_csv[, 6], lineorder_csv[, 13]);
lineorder_matrix_min = as.matrix(lineorder_csv_min);

# Extracted: COL-1 | COL-5
# => D_DATEKEY | D_YEAR
date_csv_min = cbind(date_csv[, 1], date_csv[, 5]);
date_matrix_min = as.matrix(date_csv_min);

# -- Filter tables over string values.

# Prepare PART table on-the-fly encodings
# Extracted: COL-1 | COL-5
# P_PARTKEY | P_BRAND
# (only need P_BRAND encoding, filter by P_BRAND string itself)
[part_brand_enc_f, part_brand_meta] = transformencode(target=part_csv[,5], spec=general_spec);
#print(toString(part_brand_enc_f));

part_filt_keys = matrix(0, rows=0, cols=1);
part_filt_brand = matrix(0, rows=0, cols=1);
part_filt = matrix(0, rows=0, cols=1);

# Build filtered PART table (P_BRAND BETWEEN 'MFGR#2221' AND 'MFGR#2228'), keeping key and encoded brand
for (i in 1:nrow(part_csv)) {
    p_elem = as.scalar(part_csv[i,5])
    if ( p_elem >= "MFGR#2221" & p_elem <= "MFGR#2228") {
        key_val = as.double(as.scalar(part_csv[i,1]));
        brand_code = as.double(as.scalar(part_brand_enc_f[i,1]));
        part_filt_keys = rbind(part_filt_keys, matrix(key_val, rows=1, cols=1));
        part_filt_brand = rbind(part_filt_brand, matrix(brand_code, rows=1, cols=1));
    }
}
if (nrow(part_filt_keys) == 0) {
    hasRows = 0;
}
else{
    part_filt = cbind(part_filt_keys, part_filt_brand);
}

# Extracted: COL-1 | COL-6
# S_SUPPKEY | S_REGION
supp_filt = matrix(0, rows=0, cols=1);
if(hasRows){
    # Build filtered SUPPLIER table (S_REGION = 'ASIA')
    for (i in 1:nrow(supp_csv)) {
        if (as.scalar(supp_csv[i,6]) == "ASIA") {
            key_val = as.double(as.scalar(supp_csv[i,1]));
            supp_filt = rbind(supp_filt, matrix(key_val, rows=1, cols=1));
        }
    }
    if (nrow(supp_filt) == 0) {
        hasRows = 0;
    }
}

#print("LO,DATE,PART,SUPP")
#print(toString(lineorder_matrix_min[1,]))
#print(toString(date_matrix_min[1,]))
#print(toString(part_filt[1,]))
#print(toString(supp_filt[1,]))

# -- JOIN TABLES WITH RA-JOIN FUNCTION --

# Join LINEORDER table with PART, SUPPLIER, DATE tables (star schema)
# Join order does matter!
lo_part = matrix(0, rows=0, cols=1);
lo_part_supp = matrix(0, rows=0, cols=1);
joined_matrix = matrix(0, rows=0, cols=1);
# WHERE LO_PARTKEY = P_PARTKEY
if(hasRows){
    lo_part = raJoin::m_raJoin(A=part_filt, colA=1, B=lineorder_matrix_min, colB=1, method="hash2");
    if(nrow(lo_part[,1]) == 0){
        hasRows = 0;
    }
}
# WHERE LO_SUPPKEY = S_SUPPKEY
if(hasRows){
    lo_part_supp = raJoin::m_raJoin(A=supp_filt, colA=1, B=lo_part, colB=4, method="hash2");
    if(nrow(lo_part_supp[,1]) == 0){
        hasRows = 0;
    }
}
# WHERE LO_ORDERDATE = D_DATEKEY
# (D_DATEKEY | D_YEAR) | (S_SUPPKEY | P_PARTKEY | P_BRAND | LO_PARTKEY | LO_SUPPKEY | LO_ORDERDATE | LO_REVENUE)
if(hasRows){
    joined_matrix = raJoin::m_raJoin(A=date_matrix_min, colA=1, B=lo_part_supp, colB=6, method="hash2");
    if(nrow(joined_matrix[,1]) == 0){
        hasRows = 0;
    }
}
#print(toString(joined_matrix[1,]))

# -- GROUP-BY & AGGREGATION --

# -- Group-By and Aggregation (SUM)--

if(hasRows){
    # Group-By
    d_year = joined_matrix[,2]
    p_brand = joined_matrix[,5]
    lo_revenue = joined_matrix[,9]

    # CALCULATING COMBINATION KEY WITH PRIORITY:1 D_YEAR, 2 P_BRAND

    max_p_brand = max(p_brand);
    max_d_year = max(d_year);

    p_brand_scale_f = ceil(max_p_brand) + 1;
    d_year_scale_f = ceil(max_d_year) + 1;

    combined_key = d_year * p_brand_scale_f + p_brand;

    group_input = cbind(lo_revenue, combined_key)

    agg_result = raGrp::m_raGroupby(X=group_input, col=2, method="nested-loop");

    # Aggregation (SUM)
    key = agg_result[, 1];
    revenue = rowSums(agg_result[, 2:ncol(agg_result)]);

    # EXTRACTING D_YEAR, P_BRAND
    d_year = round(floor(key / (p_brand_scale_f)));
    p_brand = round(key %% p_brand_scale_f);
    result = cbind(revenue, d_year, p_brand, key);

    # -- Sorting -- -- Sorting int columns works, but string does not.
    # ORDER BY D_YEAR, P_BRAND ASC
    result_ordered = order(target=result, by=4, decreasing=FALSE, index.return=FALSE);

    p_brand_dec = transformdecode(target=result_ordered[,3], spec=general_spec, meta=part_brand_meta);
    res = cbind(as.frame(result_ordered[,1]), as.frame(result_ordered[,2]), p_brand_dec) ;

    # Print result
    print("SUM(lo_revenue) | d_year | p_brand")
    print(res)

    print("\nQ2.2 finished.\n");
}
else{
    # If the result table has 0 rows, skip group-by and aggregation.
    # Print result
    print("SUM(lo_revenue) | d_year | p_brand")
    print("The result table has 0 rows.")

    print("\nQ2.2 finished.\n");
}