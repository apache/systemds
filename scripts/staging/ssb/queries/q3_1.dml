#-------------------------------------------------------------
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
#-------------------------------------------------------------

/* DML-script implementing the ssb query Q3.1 in SystemDS.
**input_dir="/scripts/ssb/data"

* Run with docker:
docker run -it --rm -v $PWD:/scripts/ apache/systemds:nightly -f /scripts/queries/q3_1.dml -nvargs input_dir="/scripts/data/"

SELECT
    c_nation,
    s_nation,
    d_year,
    SUM(lo_revenue) AS REVENUE
FROM customer, lineorder, supplier, date --dates
WHERE
    lo_custkey = c_custkey
    AND lo_suppkey = s_suppkey
    AND lo_orderdate = d_datekey
    AND c_region = 'ASIA'
    AND s_region = 'ASIA'
    AND d_year >= 1992
    AND d_year <= 1997
GROUP BY c_nation, s_nation, d_year
ORDER BY d_year ASC, REVENUE DESC;

*Please run the original SQL query (eg. in Postgres) 
to verify the correctness of DML version.
-> First tests: Works on the dataset with scale factor 0.1.
-> Sorting does not work.

*Based on older implementations.
https://github.com/ghafek/systemds/blob/feature/ssb-benchmark/scripts/ssb/queries/q1_1.dml
*Especially:
https://github.com/ghafek/systemds/blob/feature/ssb-benchmark/scripts/ssb/queries/q4_3.dml
In comparison to older version the join method was changed
from sort-merge to hash2 to improve the performance.

Input parameters:
input_dir - Path to input directory containing the table files (e.g., ./data)
*/

# Call ra-modules with ra-functions.
source("./scripts/builtin/raSelection.dml") as raSel
source("./scripts/builtin/raJoin.dml") as raJoin
source("./scripts/builtin/raGroupby.dml") as raGrp

# Set input parameters.
input_dir = ifdef($input_dir, "./data");
print("Loading tables from directory: " + input_dir);

# Read and load input CSV files.  
lineorder_csv = read(input_dir + "/lineorder.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
cust_csv = read(input_dir + "/customer.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
date_csv = read(input_dir + "/date.tbl", data_type="frame", format="csv", header=FALSE, sep="|");
supp_csv = read(input_dir + "/supplier.tbl", data_type="frame", format="csv", header=FALSE, sep="|");

# General variables.
general_spec = "{ \"ids\": false, \"recode\": [\"C1\"] }";
hasRows = 1; # If hasRows = 0, the result table is empty.

# -- Data preparation --

# Extract only the necessary columns from tables.
# Extracted: COL-3 | COL-5 | COL-6 | COL-13
# => LO_CUSTKEY | LO_SUPPKEY | LO_ORDERDATE | LO_REVENUE
lineorder_csv_min = cbind(lineorder_csv[, 3], lineorder_csv[, 5], lineorder_csv[, 6], lineorder_csv[, 13]);
lineorder_matrix_min = as.matrix(lineorder_csv_min);

# Extracted: COL-1 | COL-5
# => D_DATEKEY | D_YEAR
date_csv_min = cbind(date_csv[, 1], date_csv[, 5]);
date_matrix_min = as.matrix(date_csv_min);

# -- Filter tables over string values.

# WHERE D_YEAR >= 1992 AND D_YEAR <= 1997
d_filt = raSel::m_raSelection(date_matrix_min, col=2, op=">=", val=1992);
d_filt = raSel::m_raSelection(d_filt, col=2, op="<=", val=1997);
if( as.scalar(d_filt[1,1]) == 0){
    hasRows = 0;
}

# Prepare SUPPLIER table on-the-fly encodings
# Extracted: COL-1 | COL-5 | COL-6
# S_SUPPKEY | S_NATION | S_REGION
# (only need S_NATION encoding, filter by S_REGION string)
[supp_nat_enc_f, supp_nat_meta] = transformencode(target=supp_csv[,5], spec=general_spec);

supp_filt_keys = matrix(0, rows=0, cols=1);
supp_filt_nat = matrix(0, rows=0, cols=1);
supp_filt = matrix(0, rows=0, cols=1);

if(hasRows){
    # Build filtered SUPPLIER table (S_REGION == 'ASIA')
    for (i in 1:nrow(supp_csv)) {
        if (as.scalar(supp_csv[i,6]) == "ASIA") {
            key_val = as.double(as.scalar(supp_csv[i,1]));
            nat_code = as.double(as.scalar(supp_nat_enc_f[i,1]));
            supp_filt_keys = rbind(supp_filt_keys, matrix(key_val, rows=1, cols=1));
            supp_filt_nat = rbind(supp_filt_nat, matrix(nat_code, rows=1, cols=1));
        }
    }
    if (nrow(supp_filt_keys) == 0) {
        hasRows = 0;
    }
    else{
        supp_filt = cbind(supp_filt_keys, supp_filt_nat);
    }
}

# Prepare CUSTOMER table on-the-fly encodings
# Extracted: COL-1 | COL-5 | COL-6
# C_CUSTKEY | C_NATION | C_REGION
# (only need C_NATION encoding, filter by C_REGION string)
[cust_nat_enc_f, cust_nat_meta] = transformencode(target=cust_csv[,5], spec=general_spec);

cust_filt_keys = matrix(0, rows=0, cols=1);
cust_filt_nat = matrix(0, rows=0, cols=1);
cust_filt = matrix(0, rows=0, cols=1);

if(hasRows){
    # Build filtered CUSTOMER table (C_REGION = 'ASIA')
    for (i in 1:nrow(cust_csv)) {
        if (as.scalar(cust_csv[i,6]) == "ASIA") {
            key_val = as.double(as.scalar(cust_csv[i,1]));
            nat_code = as.double(as.scalar(cust_nat_enc_f[i,1]));
            cust_filt_keys = rbind(cust_filt_keys, matrix(key_val, rows=1, cols=1));
            cust_filt_nat = rbind(cust_filt_nat, matrix(nat_code, rows=1, cols=1));
        }
    }
    if (nrow(cust_filt_keys) == 0) {
        hasRows = 0;
    }
    else{
        cust_filt = cbind(cust_filt_keys,cust_filt_nat);
    }
}
#print("LO,DATE,CUST,SUPP")
#print(toString(lineorder_matrix_min[1,]))
#print(toString(date_matrix_min[1,]))
#print(toString(cust_filt[1,]))
#print(toString(supp_filt[1,]))


# -- JOIN TABLES WITH RA-JOIN FUNCTION --

# Join LINEORDER table with CUST, SUPPLIER, DATE tables (star schema)
# Join order does matter! 
lo_cust = matrix(0, rows=0, cols=1);
lo_cust_supp = matrix(0, rows=0, cols=1);
joined_matrix = matrix(0, rows=0, cols=1);
# WHERE LO_CUSTKEY = C_CUSTKEY
if(hasRows){
    lo_cust = raJoin::m_raJoin(A=cust_filt, colA=1, B=lineorder_matrix_min, colB=1, method="hash2");
    if(nrow(lo_cust[,1]) == 0){
        hasRows = 0;
    }
}
# WHERE LO_SUPPKEY = S_SUPPKEY
if(hasRows){
    lo_cust_supp = raJoin::m_raJoin(A=supp_filt, colA=1, B=lo_cust, colB=4, method="hash2");
    if(nrow(lo_cust_supp[,1]) == 0){
        hasRows = 0;
    }
}
# WHERE LO_ORDERDATE = D_DATEKEY
# (D_DATEKEY | D_YEAR) | (S_SUPPKEY | S_NATION | C_CUSTKEY | C_NATION |
# LO_CUSTKEY | LO_SUPPKEY | LO_ORDERDATE | LO_REVENUE)
if(hasRows){
    joined_matrix = raJoin::m_raJoin(A=d_filt, colA=1, B=lo_cust_supp, colB=7, method="hash2");
    #print(toString(joined_matrix[1,]))
    if(nrow(joined_matrix[,1]) == 0){
        hasRows = 0;
    }
}
#print(toString(joined_matrix[1,]))

# -- Group-By and Aggregation (SUM)--
if(hasRows){
    # Group-By
    d_year = joined_matrix[,2];
    s_nat = joined_matrix[,4];
    c_nat = joined_matrix[,6];
    revenue = joined_matrix[,10];

    # CALCULATING COMBINATION KEY WITH PRIORITY:1 C_NATION, 2 S_NATION, D_YEAR
    max_c_nat= max(c_nat);
    max_s_nat= max(s_nat);
    max_d_year = max(d_year);

    c_nat_scale_f = ceil(max_c_nat) + 1;
    s_nat_scale_f = ceil(max_s_nat) + 1;
    d_year_scale_f = ceil(max_d_year) + 1;

    combined_key = c_nat * s_nat_scale_f * d_year_scale_f + s_nat * d_year_scale_f + d_year;

    group_input = cbind(revenue, combined_key)

    agg_result = raGrp::m_raGroupby(X=group_input, col=2, method="nested-loop");
    #print(toString(agg_result[1,]));

    # Aggregation (SUM)
    key = agg_result[, 1];
    revenue = rowSums(agg_result[, 2:ncol(agg_result)]);

    # EXTRACTING C_NATION, S_NATION, D_YEAR
    c_nat = round(floor(key / (s_nat_scale_f * d_year_scale_f)));
    s_nat = round(floor((key %% (s_nat_scale_f * d_year_scale_f)) / d_year_scale_f));
    d_year = round(key %% d_year_scale_f);

    result = cbind(c_nat, s_nat, d_year, revenue, key)

    # -- Sorting -- -- Sorting int columns works, but strings do not.
    # ORDER BY D_YEAR ASC, REVENUE DESC
    result_ordered = order(target=result, by=4, decreasing=TRUE, index.return=FALSE);
    result_ordered = order(target=result_ordered, by=3, decreasing=FALSE, index.return=FALSE);

    c_nat_dec = transformdecode(target=result_ordered[,1], spec=general_spec, meta=cust_nat_meta);
    s_nat_dec = transformdecode(target=result_ordered[,2], spec=general_spec, meta=supp_nat_meta);

    res = cbind(c_nat_dec, s_nat_dec, as.frame(result_ordered[,3]), as.frame(result_ordered[,4])) ;

    # Print result
    print("c_nation | s_nation | d_year | REVENUE")
    print(res)

    print("\nQ3.1 finished.\n");
}
else{
    # If the result table has 0 rows, skip group-by and aggregation.
    # Print result
    print("c_nation | s_nation | d_year | REVENUE")
    print("The result table has 0 rows.")
    print("\nQ3.1 finished.\n");    
}